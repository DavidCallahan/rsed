%{
#include <string>
#include <fstream>
#include "AST.h"
#include "Scanner.h"
#include "grammar.tab.hpp"

static std::string * makeString(const char * text) {
    return new std::string(text);
}

// invoked in grammar.tab.cpp
int yylex(Scanner *scanner) { return scanner->yylex(); }

#define register 

%}

%option yylineno
%option noyywrap

DIGIT    [0-9]
ID       [a-zA-Z][a-zA-Z0-9_]*

%%

all	    return ALL;
columns	    return COLUMNS;
copy        return COPY;
else	    return ELSE;
end         return END;
error	    return ERROR;
for	    return FOR;
foreach     return FOREACH;
if	    return IF;
in	    return IN;
input	    return INPUT;
not	    return NOT_TOK;
output	    return OUTPUT;
past	    return PAST;
print	    return PRINT;
replace     return REPLACE_TOK;
skip        return SKIP;
style	    return STYLE;
then	    return THEN;
to	    return TO;
with	    return WITH;
split	    return SPLIT;
required    return REQUIRED;
close	    return CLOSE;
match	    return MATCH_KW;

{ID}	    yylval.name = new std::string(yytext);   return IDENTIFIER;
\${ID}	    yylval.name = new std::string(yytext+1); return VARIABLE;
\${DIGIT}+  yylval.integer = atoi(yytext+1); return DYN_VARIABLE;
{DIGIT}+    yylval.integer = atoi(yytext) ; return NUMBER;
\$\(	    return LOOKUP_START;

=~	    return MATCH_TOK;
=	    return '=';
\(	    return '(';
\)	    return ')';
,	    return ',';
;	    return NEWLINE;
&.*\n	    /* comment */
"=="	    return EQ_TOK;
"!="	    return NE_TOK;
"<>"	    return NE_TOK;
"<"	    return '<';
">"	    return '>';
"<="	    return LE_TOK;
">="	    return GE_TOK;
"+"	    return '+';
"-"	    return '-';
"*"	    return '*';
"/"	    return '/';
	    	   
#.*         /* comment */
[ \t]+          /* eat up whitespace */
"<<"        { yylval.name = multilineString(); unput('\n'); return STRING; }

\n([ \t]*(#.*)?\n)* return NEWLINE;

\"([^"]|\\\")*\"[rig]*  yylval.name = makeString(yytext); return STRING;
\'([^']|\\\')*\'[rig]*  yylval.name = makeString(yytext); return STRING;

<<EOF>>     { static int seen = 0; return (seen++ ? 0 : NEWLINE); }

.           printf( "Unrecognized character: %s\n", yytext );

%%
